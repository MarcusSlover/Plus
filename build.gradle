plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.marcusslover'
version = '4.4.0-SNAPSHOT'

ext {
    isRelease = !version.toString().endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.1'
    compileOnly 'io.papermc.paper:paper-api:1.21.6-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.3.0'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'io.papermc.paper:paper-api:1.21.6-R0.1-SNAPSHOT'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    options.encoding = 'UTF-8'
}
tasks.named('javadoc') {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'plus'
            version = project.version

            pom {
                name = 'Plus'
                description = 'A set of tools used for programming Minecraft plugins.'
                url = 'https://github.com/MarcusSlover/Plus'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/MarcusSlover/Plus/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'marcusslover'
                        name = 'MarcusSlover'
                        email = 'jr.marcusorlando@gmail.com'
                    }
                }
                contributors {
                    contributor {
                        name = "tecc"
                        email = "tecc@tecc.me"
                        url = "https://tecc.me"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "centralPortal"
            url = uri("https://central.sonatype.com/repository/maven-snapshots/")

            credentials {
                username = findProperty("portalUsername") ?: System.getenv("PORTAL_USERNAME")
                password = findProperty("portalPassword") ?: System.getenv("PORTAL_PASSWORD")
            }
        }
    }
}
